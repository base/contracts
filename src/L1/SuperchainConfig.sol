// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

// Contracts
import { ProxyAdminOwnedBase } from "src/L1/ProxyAdminOwnedBase.sol";

// Interfaces
import { ISemver } from "interfaces/universal/ISemver.sol";

/// @custom:proxied true
/// @title SuperchainConfig
/// @notice The SuperchainConfig contract is used to manage configuration of global superchain values.
/// @dev WARNING: When upgrading this contract, any active pause states will be lost as the pause state
///      is stored in storage variables that are not preserved during upgrades. Therefore, this contract
///      should not be upgraded while the system is paused.
contract SuperchainConfig is ProxyAdminOwnedBase, ISemver {
    /// @notice Thrown when a caller is not the guardian but tries to call a guardian-only function
    error SuperchainConfig_OnlyGuardian();

    /// @notice Thrown when a caller is not the guardian or incident responder but tries to pause
    error SuperchainConfig_OnlyGuardianOrIncidentResponder();

    /// @notice Thrown when attempting to pause an identifier that is already paused
    error SuperchainConfig_AlreadyPaused(address identifier);

    /// @notice Thrown when attempting to extend a pause that is not already paused.
    error SuperchainConfig_NotAlreadyPaused(address identifier);

    /// @notice The duration after which a pause expires. This value is set to exactly 3 months in
    ///         seconds. Any duration longer than this value is incompatible with Stage 1.
    uint256 internal constant PAUSE_EXPIRY = 7_884_000;

    /// @notice The address of the guardian, which can pause withdrawals from the System.
    ///         This is an immutable variable set at construction time.
    address public immutable GUARDIAN;

    /// @notice The address of the incident responder, which can pause the system.
    ///         This is an immutable variable set at construction time.
    address public immutable INCIDENT_RESPONDER;

    /// @notice Mapping of pause identifiers to their pause timestamps
    mapping(address => uint256) public pauseTimestamps;

    /// @notice Emitted when the pause is triggered.
    /// @param identifier An address helping to identify provenance of the pause transaction.
    event Paused(address identifier);

    /// @notice Emitted when the pause is lifted.
    event Unpaused(address identifier);

    /// @notice Emitted when a pause is extended.
    /// @param identifier An address helping to identify provenance of the pause transaction.
    event PauseExtended(address identifier);

    /// @notice Semantic version.
    /// @custom:semver 2.5.0
    string public constant version = "2.5.0";

    /// @notice Constructs the SuperchainConfig contract.
    /// @param _guardian The address of the guardian, which can pause and unpause the system.
    /// @param _incidentResponder The address of the incident responder, which can pause the system.
    constructor(address _guardian, address _incidentResponder) {
        GUARDIAN = _guardian;
        INCIDENT_RESPONDER = _incidentResponder;
    }

    /// @notice Getter for the guardian address.
    /// @return The guardian address.
    function guardian() external view returns (address) {
        return GUARDIAN;
    }

    /// @notice Getter for the incident responder address.
    /// @return The incident responder address.
    function incidentResponder() external view returns (address) {
        return INCIDENT_RESPONDER;
    }

    /// @notice Returns the duration after which a pause expires.
    /// @return The duration after which a pause expires.
    function pauseExpiry() external pure returns (uint256) {
        return PAUSE_EXPIRY;
    }

    /// @notice Pauses the system for a specific superchain cluster identifier.
    /// @param _identifier The address identifier for the pause.
    function pause(address _identifier) external {
        // Only the Guardian or Incident Responder can pause the system.
        _assertOnlyGuardianOrIncidentResponder();

        // Cannot pause if the identifier is already paused to prevent re-pausing without either
        // unpausing, extending, or resetting the pause timestamp. Note that this check intentionally
        // prevents re-pausing even after a pause has expired (when paused() returns false but the
        // timestamp is still non-zero). This is a Stage 1 Decentralization requirement: the guardian
        // must explicitly unpause before pausing again, ensuring deliberate action is taken.
        if (pauseTimestamps[_identifier] != 0) {
            revert SuperchainConfig_AlreadyPaused(_identifier);
        }

        // Set the pause timestamp.
        pauseTimestamps[_identifier] = block.timestamp;
        emit Paused(_identifier);
    }

    /// @notice Unpauses the system for a specific identifier.
    /// @param _identifier The address identifier to unpause.
    function unpause(address _identifier) external {
        // Only the Guardian can unpause the system.
        _assertOnlyGuardian();

        // Unpause the system.
        pauseTimestamps[_identifier] = 0;
        emit Unpaused(_identifier);
    }

    /// @notice Extends the pause for a specific identifier by resetting the pause timestamp.
    /// @param _identifier The address identifier to extend.
    function extend(address _identifier) external {
        // Only the Guardian can extend the pause.
        _assertOnlyGuardian();

        // Cannot extend the pause if not already paused.
        if (pauseTimestamps[_identifier] == 0) {
            revert SuperchainConfig_NotAlreadyPaused(_identifier);
        }

        // Reset the pause timestamp.
        pauseTimestamps[_identifier] = block.timestamp;
        emit PauseExtended(_identifier);
    }

    /// @notice Checks if the system can be paused for a specific identifier.
    /// @param _identifier The address identifier to check.
    /// @return True if the system can be paused for this identifier.
    function pausable(address _identifier) external view returns (bool) {
        return pauseTimestamps[_identifier] == 0;
    }

    /// @custom:legacy
    /// @notice Checks if the global superchain system is paused. NOTE that this is a legacy
    ///         function that provides support for systems that still rely on the older interface.
    ///         Contracts should use paused(address) instead when possible.
    /// @return True if the global superchain system is paused.
    function paused() external view returns (bool) {
        return paused(address(0));
    }

    /// @notice Checks if the system is currently paused for a specific identifier.
    /// @param _identifier The address identifier to check.
    /// @return True if the system is paused for this identifier and not expired.
    function paused(address _identifier) public view returns (bool) {
        uint256 timestamp = pauseTimestamps[_identifier];
        if (timestamp == 0) return false;
        return block.timestamp < timestamp + PAUSE_EXPIRY;
    }

    /// @notice Gets the expiration timestamp for a specific pause identifier.
    /// @param _identifier The address identifier to check.
    /// @return The timestamp when the pause expires, or 0 if not paused.
    function expiration(address _identifier) external view returns (uint256) {
        uint256 timestamp = pauseTimestamps[_identifier];
        if (timestamp == 0) return 0;
        return timestamp + PAUSE_EXPIRY;
    }

    /// @notice Asserts that the caller is the guardian.
    function _assertOnlyGuardian() internal view {
        if (msg.sender != GUARDIAN) {
            revert SuperchainConfig_OnlyGuardian();
        }
    }

    /// @notice Asserts that the caller is the guardian or incident responder.
    function _assertOnlyGuardianOrIncidentResponder() internal view {
        if (msg.sender != GUARDIAN && msg.sender != INCIDENT_RESPONDER) {
            revert SuperchainConfig_OnlyGuardianOrIncidentResponder();
        }
    }
}
